rule Remcos
{
	meta:
		author = "kevoreilly"
		description = "Remcos Payload"
		cape_type = "Remcos Payload"
		ruleset = "Remcos.yar"
		repository = "kevoreilly/CAPEv2"
		source_url = "https://github.com/kevoreilly/CAPEv2/blob/9c8d6da44b595f8140a5cd76edd8101f6812c3b0/data/yara/CAPE/Remcos.yar"
		license = "Other"
		score = 75

	strings:
		$name = "Remcos" nocase
		$time = "%02i:%02i:%02i:%03i"
		$crypto1 = {81 E1 FF 00 00 80 79 ?? 4? 81 C9 00 FF FF FF 4? 8A ?4 8?}
		$crypto2 = {0F B6 [1-7] 8B 45 08 [0-2] 8D 34 07 8B 01 03 C2 8B CB 99 F7 F9 8A 84 95 ?? ?? FF FF 30 06 47 3B 7D 0C 72}

	condition:
		uint16(0)==0x5A4D and 
		($name) and 
		($time) and 
		any of ($crypto*)
}

rule Windows_Trojan_Remcos_b296e965
{
	meta:
		author = "Elastic Security"
		id = "b296e965-a99e-4446-b969-ba233a2a8af4"
		fingerprint = "a5267bc2dee28a3ef58beeb7e4a151699e3e561c16ce0ab9eb27de33c122664d"
		creation_date = "2021-06-10"
		last_modified = "2021-08-23"
		threat_name = "Windows.Trojan.Remcos"
		reference = "https://www.elastic.co/security-labs/exploring-the-ref2731-intrusion-set"
		reference_sample = "0ebeffa44bd1c3603e30688ace84ea638fbcf485ca55ddcfd6fbe90609d4f3ed"
		severity = 100
		arch_context = "x86"
		scan_context = "file, memory"
		license = "Elastic License v2"
		os = "windows"
		ruleset = "Windows_Trojan_Remcos.yar"
		repository = "elastic/protections-artifacts"
		source_url = "https://github.com/elastic/protections-artifacts/blob/3bbef930abab9814b2fdb4704be075ab1daf2ea0/yara/rules/Windows_Trojan_Remcos.yar"
		score = 75

	strings:
		$a1 = "Remcos restarted by watchdog!" ascii fullword
		$a2 = "Mutex_RemWatchdog" ascii fullword
		$a3 = "%02i:%02i:%02i:%03i"
		$a4 = "* Remcos v" ascii fullword

	condition:
		2 of them
}

rule Windows_Trojan_Remcos_7591e9f1
{
	meta:
		author = "Elastic Security"
		id = "7591e9f1-452d-4731-9bec-545fb0272c80"
		fingerprint = "9436c314f89a09900a9b3c2fd9bab4a0423912427cf47b71edce5eba31132449"
		creation_date = "2023-06-23"
		last_modified = "2023-07-10"
		threat_name = "Windows.Trojan.Remcos"
		reference = "https://www.elastic.co/security-labs/exploring-the-ref2731-intrusion-set"
		reference_sample = "4e6e5ecd1cf9c88d536c894d74320c77967fe08c75066098082bf237283842fa"
		severity = 100
		arch_context = "x86"
		scan_context = "file, memory"
		license = "Elastic License v2"
		os = "windows"
		ruleset = "Windows_Trojan_Remcos.yar"
		repository = "elastic/protections-artifacts"
		source_url = "https://github.com/elastic/protections-artifacts/blob/3bbef930abab9814b2fdb4704be075ab1daf2ea0/yara/rules/Windows_Trojan_Remcos.yar"
		score = 75

	strings:
		$a1 = "ServRem" ascii fullword
		$a2 = "Screenshots" ascii fullword
		$a3 = "MicRecords" ascii fullword
		$a4 = "remcos.exe" wide nocase fullword
		$a5 = "Remcos" wide fullword
		$a6 = "logs.dat" wide fullword

	condition:
		3 of them
}

rule malware_Remcos_strings
{
	meta:
		description = "detect Remcos in memory"
		author = "JPCERT/CC Incident Response Group"
		rule_usage = "memory scan"
		hash1 = "7d5efb7e8b8947e5fe1fa12843a2faa0ebdfd7137582e5925a0b9c6a9350b0a5"
		ruleset = "Remcos.yara"
		repository = "JPCERTCC/jpcert-yara"
		source_url = "https://github.com/JPCERTCC/jpcert-yara/blob/0722a9365ec6bc969c517c623cd166743d1bc473/other/Remcos.yara"
		license = "Other"
		score = 75

	strings:
		$remcos = "Remcos" ascii fullword
		$url1 = "Breaking-Security.Net" ascii fullword
		$url2 = "BreakingSecurity.Net" ascii fullword
		$resource = "SETTINGS" ascii wide fullword

	condition:
		1 of ($url*) and 
		$remcos and 
		$resource
}

rule win_remcos_auto
{
	meta:
		author = "Felix Bilstein - yara-signator at cocacoding dot com"
		date = "2023-12-06"
		version = "1"
		description = "Detects win.remcos."
		info = "autogenerated rule brought to you by yara-signator"
		tool = "yara-signator v0.6.0"
		signator_config = "callsandjumps;datarefs;binvalue"
		malpedia_reference = "https://malpedia.caad.fkie.fraunhofer.de/details/win.remcos"
		malpedia_rule_date = "20231130"
		malpedia_hash = "fc8a0e9f343f6d6ded9e7df1a64dac0cc68d7351"
		malpedia_version = "20230808"
		malpedia_license = "CC BY-SA 4.0"
		malpedia_sharing = "TLP:WHITE"
		ruleset = "win.remcos_auto.yar"
		repository = "malpedia/signator-rules"
		source_url = "https://github.com/malpedia/signator-rules/blob/fbacfc09b84d53d410385e66a8e56f25016c588a/rules/win.remcos_auto.yar"
		score = 75

	strings:
		$sequence_0 = { 7410 6a00 ff35???????? ff15???????? }
		$sequence_1 = { 50 ff15???????? 8d45f0 33f6 }
		$sequence_2 = { 6a09 ff35???????? ff15???????? ff35???????? ff15???????? }
		$sequence_3 = { 8d45f8 50 ff15???????? ff7508 }
		$sequence_4 = { 7508 ff15???????? 33c0 5f }
		$sequence_5 = { 6a09 ff35???????? ff15???????? ff35???????? }
		$sequence_6 = { ff15???????? 50 ff15???????? 8d45f0 33f6 }
		$sequence_7 = { 50 6a28 ff15???????? 50 ff15???????? 8d45f0 33f6 }
		$sequence_8 = { 51 51 8d45f8 c745f808000000 50 ff15???????? ff15???????? }
		$sequence_9 = { 85c0 7410 6a00 ff35???????? ff15???????? }

	condition:
		7 of them and 
		filesize <1054720
}

rule Remcos_1
{
	meta:
		author = "@neonprimetime"
		description = "Remcos RAT"
		original_yara_name = "Remcos"
		ruleset = "remcos.yar"
		repository = "kevthehermit/RATDecoders"
		source_url = "https://github.com/kevthehermit/RATDecoders/blob/d675ba1c06e6dd8365149c9ee8a8db1a6e5e508e/malwareconfig/yaraRules/remcos.yar"
		license = "MIT License"
		score = 75

	strings:
		$a1 = "Software\\Remcos"
		$a2 = "\\remcos\\"
		$a3 = "REMCOS v"
		$b1 = "Keylogger Started"
		$b2 = "Connected to C&C"
		$b3 = "Screenshots"
		$b4 = "OpenCamera"
		$b5 = "Uploading file to C&C"
		$b6 = "Initializing connection to C&C"
		$b7 = "cleared!]"
		$b8 = "EnableLUA /t REG_DWORD /d 0"
		$b9 = "Uploading file to C&C"
		$b10 = "%02i:%02i:%02i:%03i"
		$b11 = "[Firefox StoredLogins Cleared!]"
		$b12 = "licence_code.txt"

	condition:
		1 of ($a*) or 
		3 of ($b*)
}

rule MAL_Remcos_strings
{
	meta:
		description = "Matches strings found in Remcos RAT samples."
		last_modified = "2024-03-20"
		author = "@petermstewart"
		DaysofYara = "80/100"
		sha256 = "b3d7fad59a0ae75ffef9e05f47fc381b4adb716c498106482492e56c1b4370a7"
		sha256 = "9046b2e6ce92647474048c30439ab21ee69a46f6067dbaff67de729644120fad"
		ruleset = "MAL_C2_Remcos.yar"
		repository = "100DaysofYARA/2024"
		source_url = "https://github.com/100DaysofYARA/2024/blob/7df92fafb900e3f148d927ac8dd68bfeaea0c332/petermstewart/MAL_C2_Remcos.yar"
		license = "MIT License"
		score = 75

	strings:
		$a = "Remcos_Mutex_Inj"
		$b1 = "Uploading file to C&C: "
		$b2 = "Unable to delete: "
		$b3 = "Unable to rename file!"
		$b4 = "Browsing directory: "
		$b5 = "Offline Keylogger Started"
		$b6 = "Online Keylogger Started"
		$b7 = "[Chrome StoredLogins found, cleared!]"
		$b8 = "[Firefox StoredLogins cleared!]"
		$b9 = "Cleared all browser cookies, logins and passwords."
		$b10 = "[Following text has been pasted from clipboard:]"
		$b11 = "[End of clipboard text]"
		$b12 = "OpenCamera"
		$b13 = "CloseCamera"

	condition:
		uint16(0)==0x5a4d and 
		$a and 
		10 of ($b*)
}

rule malware_windows_remcos_rat
{
	meta:
		description = "https://blog.fortinet.com/2017/02/14/remcos-a-new-rat-in-the-wild-2"
		reference = "https://breaking-security.net/remcos/remcos-changelog/"
		author = "@mimeframe"
		md5 = "c8dafe143fe1d81ae6a3c0cd4724b272"
		ruleset = "malware_windows_remcos_rat.yara"
		repository = "airbnb/binaryalert"
		source_url = "https://github.com/airbnb/binaryalert/blob/a9c0f06affc35e1f8e45bb77f835b92350c68a0b/rules/public/malware/windows/malware_windows_remcos_rat.yara"
		license = "Apache License 2.0"
		score = 75

	strings:
		$a1 = "[Following text has been pasted from clipboard:]" wide ascii
		$a2 = "[Chrome StoredLogins found, cleared!]" wide ascii
		$a3 = "[Firefox StoredLogins cleared!]" wide ascii
		$b1 = "getclipboard" wide ascii
		$b2 = "stopmiccapture" wide ascii
		$b3 = "downloadfromurltofile" wide ascii
		$b4 = "getcamsingleframe" wide ascii
		$c1 = "Breaking-Security.Net" wide ascii
		$c2 = "REMCOS v" wide ascii

	condition:
		any of ($a*) or 
		3 of ($b*) or 
		all of ($c*)
}

rule win_remcos_rat_unpacked
{
	meta:
		author = "Matthew @ Embee_Research"
		created = "2023/08/27"
		description = "Detects strings present in remcos rat Samples."
		sha_256 = "ec901217558e77f2f449031a6a1190b1e99b30fa1bb8d8dabc3a99bc69833784"
		ruleset = "win_remcos_rat_unpacked_aug_2023.yar"
		repository = "embee-research/Yara-detection-rules"
		source_url = "https://github.com/embee-research/Yara-detection-rules/blob/ac56d6f6fd2a30c8cb6e5c0455d6519210a8b0f4/Rules/win_remcos_rat_unpacked_aug_2023.yar"
		score = 75

	strings:
		$r0 = " ______                              " ascii
		$r1 = "(_____ \\                             " ascii
		$r2 = " _____) )_____ ____   ____ ___   ___ " ascii
		$r3 = "|  __  /| ___ |    \\ / ___) _ \\ /___)" ascii
		$r4 = "| |  \\ \\| ____| | | ( (__| |_| |___ |" ascii
		$r5 = "|_|   |_|_____)_|_|_|\\____)___/(___/ " ascii
		$s1 = "Watchdog module activated" ascii
		$s2 = "Remcos restarted by watchdog!" ascii
		$s3 = " BreakingSecurity.net" ascii

	condition:
		(( all of ($r*)) or 
			( all of ($s*)))
}

rule Remcos_2
{
	meta:
		description = "detect Remcos in memory"
		author = "JPCERT/CC Incident Response Group"
		rule_usage = "memory scan"
		hash1 = "7d5efb7e8b8947e5fe1fa12843a2faa0ebdfd7137582e5925a0b9c6a9350b0a5"
		original_yara_name = "Remcos"
		ruleset = "MalConfScan.yar"
		repository = "Yara-Rules/rules"
		source_url = "https://github.com/Yara-Rules/rules/blob/0f93570194a80d2f2032869055808b0ddcdfb360/malware/MalConfScan.yar"
		license = "GNU General Public License v2.0"
		score = 75

	strings:
		$remcos = "Remcos" ascii fullword
		$url = "Breaking-Security.Net" ascii fullword
		$resource = "SETTINGS" wide fullword

	condition:
		all of them
}

rule win_remcos : rat
{
	meta:
		author = "CERT Polska"

	strings:
		$convenient1 = " * Breaking-Security.Net"
		$convenient2 = " * REMCOS v"
		$convenient3 = "SETTINGS"
		$convenient4 = "Remcos_Mutex_Inj"
		$convenient5 = "Online Keylogger Started"
		$convenient6 = "Uploading file to C&C"
		$convenient7 = "Remcos Agent initialized"

	condition:
		3 of ($convenient*)
}

rule win_remcos_auto_1
{
	meta:
		author = "Felix Bilstein - yara-signator at cocacoding dot com"
		date = "2019-07-05"
		version = "1"
		description = "autogenerated rule brought to you by yara-signator"
		tool = "yara-signator 0.2a"
		malpedia_reference = "https://malpedia.caad.fkie.fraunhofer.de/details/win.remcos"
		malpedia_version = "20190620"
		malpedia_license = "CC BY-SA 4.0"
		malpedia_sharing = "TLP:WHITE"
		score = 75

	strings:
		$sequence_0 = { 51 50 8bce e8???????? 8b4608 ff7510 2b4508 }
		$sequence_1 = { ff35???????? ff15???????? eb?? 6a09 ff35???????? ff15???????? ff35???????? }
		$sequence_2 = { ff15???????? 57 57 57 8bd8 57 6a02 }
		$sequence_3 = { ff7114 e8???????? 5d c3 55 8bec }
		$sequence_4 = { 75?? 6a01 58 5d c3 8b4028 6a00 }
		$sequence_5 = { 8d45f0 50 ff15???????? 8d4df0 ff15???????? 8b4508 5e }
		$sequence_6 = { 50 e8???????? 834608f4 8bc7 5f 5e 5d }
		$sequence_7 = { 6a00 ff7508 ffd6 50 ff15???????? 50 }
		$sequence_8 = { c3 ff7510 ff750c ff15???????? f7d8 1ac0 fec0 }
		$sequence_9 = { e9???????? 55 8bec 51 56 8bf1 8d45ff }

	condition:
		7 of them
}

rule fsRemcos
{
	meta:
		description = "FsYARA - Malware Trends"
		vetted_family = "remcos"
		score = 75

	condition:
		Remcos or 
		Windows_Trojan_Remcos_b296e965 or 
		Windows_Trojan_Remcos_7591e9f1 or 
		malware_Remcos_strings or 
		win_remcos_auto or 
		Remcos_1 or 
		MAL_Remcos_strings or 
		malware_windows_remcos_rat or 
		win_remcos_rat_unpacked or 
		Remcos_2 or 
		win_remcos or 
		win_remcos_auto_1
}

